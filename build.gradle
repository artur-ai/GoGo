plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
    id 'org.sonarqube' version '7.2.2.6593'
}

group = 'com.maiboroda'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(23)
    }
}

ext {
    testcontainersVersion = '1.19.3'
    databaseRiderVersion = '1.43.0'
    mapstructVersion = '1.5.5.Final'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    runtimeOnly 'org.postgresql:postgresql'

    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-database-postgresql'

    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    implementation 'org.slf4j:slf4j-api'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation "org.testcontainers:testcontainers:${testcontainersVersion}"
    testImplementation "org.testcontainers:postgresql:${testcontainersVersion}"
    testImplementation "org.testcontainers:junit-jupiter:${testcontainersVersion}"
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'

    testImplementation "com.github.database-rider:rider-spring:${databaseRiderVersion}"
    testImplementation "com.github.database-rider:rider-junit5:${databaseRiderVersion}"
}

jacoco {
    toolVersion = "0.8.12"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/dto/**',
                    '**/entity/**',
                    '**/config/**',
                    '**/GoGoApplication.class'
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport

    violationRules {
        rule {
            limit {
                minimum = 0.85
            }
        }
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/dto/**',
                    '**/entity/**',
                    '**/config/**',
                    '**/GoGoApplication.class'
            ])
        }))
    }
}

sonar {
    properties {
        property "sonar.projectKey", "artur-ai_GoGo"
        property "sonar.organization", "artur-ai"
        property "sonar.host.url", "https://sonarcloud.io"

        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.java.source", "23"
        property "sonar.java.target", "23"

        property "sonar.sources", "src/main/java"
        property "sonar.tests", "src/test/java"

        property "sonar.java.binaries", "${project.layout.buildDirectory.get()}/classes/java/main"
        property "sonar.java.test.binaries", "${project.layout.buildDirectory.get()}/classes/java/test"

        property "sonar.coverage.jacoco.xmlReportPaths", "${project.layout.buildDirectory.get()}/reports/jacoco/test/jacocoTestReport.xml"

        property "sonar.exclusions", "**/dto/**, **/entity/**, **/config/**, **/GoGoApplication.java"
        property "sonar.coverage.exclusions", "**/dto/**, **/entity/**, **/config/**, **/GoGoApplication.java"

        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.verbose", "true"
    }
}

tasks.named('sonar') {
    dependsOn jacocoTestReport
}

tasks.register('verify') {
    dependsOn test, jacocoTestReport, jacocoTestCoverageVerification
    description = 'Runs tests, generates JaCoCo report, and verifies coverage'
    group = 'verification'
}